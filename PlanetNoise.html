<!DOCTYPE html>
<html lang="en">
	<head>
		<title>Planet Generator</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				background:#000;
				color: #eee;
				padding:0;
				margin:0;
				font-weight:bold;
				overflow:hidden;

				font-family:Monospace;
				font-size:13px;
				text-align:center;
			}

			#info {
				position: absolute;
				top: 0px; width: 100%;
				padding: 5px;
				z-index:100;
			}

			a {

				color: #0080ff;
			}

			b { color:orange }
		</style>

		<script src="three.min.js"></script>
		<script type='text/javascript' src='http://code.jquery.com/jquery-1.4.2.js'></script>
		<script src="Detector.js"></script>

	</head>

	<body>

		<div id="info"> <b>Planet Generator</b><br/>
		refresh page to generate new planet<br/>
		<b>WASD</b> move, <b>scroll</b> zoom, <b>Space</b> pause
		
		</div>
		
		<script id="fragmentShader" type="x-shader/x-fragment">

			uniform vec3 directionalLightColor;
			uniform vec3 directionalLightDirection;
			uniform int planetType;
			
			varying vec3 vWorldPosition;
			varying vec3 vViewPosition;
			varying vec3 vNormal;
			varying float percent;
			
			void main( void ) {
				
				
				vec3 dirDiffuse = vec3( 0.0,0.0,0.0 );
				vec3 dirSpecular = vec3( 0.0,0.0,0.0  );
				
				vec4 lDirection = viewMatrix * vec4( directionalLightDirection, 0.0 );
				vec3 dirVector = normalize( lDirection.xyz );
				
				float dirDiffuseWeight = max( dot( vNormal, dirVector ), 0.0 );
				
				//determine color
				
				
				vec3 white= vec3(1.0,1.0,1.0);
				
				vec3 uDiffuseColor = vec3(0.6,0.6,0.6);
				vec3 uSpecularColor = vec3(0.3,0.3,0.3);
				vec3 ambientLight= vec3(0.01,0.01,0.01);
				float uShininess = 2.0;
				
				
				if(planetType == 1){
					if(percent > 0.45){
						uDiffuseColor = uDiffuseColor * vec3(1.0,1.0,1.0);
					}
					else if(percent > 0.27){
						uDiffuseColor = uDiffuseColor * vec3(0.0,0.9 - percent,0.0);
					}
					else if(percent > 0.26){
						uDiffuseColor =  uDiffuseColor * vec3(1.0,0.9,.3);
					}
					else {
						uDiffuseColor = uDiffuseColor * vec3(0.0,0.0,percent/2.0+0.3);
					}
				}
				else if(planetType == 2){
					uDiffuseColor = uDiffuseColor * vec3(1.0-percent,0.5-percent,0.0);
				}
				else{
					uDiffuseColor = uDiffuseColor * vec3(0.0,percent,1.0-percent);
				}
				
				
				
				
				
				dirDiffuse += directionalLightColor* uDiffuseColor * dirDiffuseWeight;
					
				vec3 dirHalfVector = normalize( dirVector + vViewPosition );
				float dirDotNormalHalf = max( dot( vNormal, dirHalfVector ), 0.0 );
				float dirSpecularWeight = max( pow( dirDotNormalHalf, uShininess ), 0.0 );
					
				dirSpecular += directionalLightColor* uSpecularColor * dirSpecularWeight * dirDiffuseWeight;
					
				
				vec3 totalDiffuse = vec3( 0.0 );
				vec3 totalSpecular = vec3( 0.0 );
				
				totalDiffuse += dirDiffuse;
				totalSpecular += dirSpecular;
				
				gl_FragColor =  vec4((totalDiffuse + ambientLight + totalSpecular),1.0);
				//gl_FragColor =  vec4(percent,0,0,1.0);
			}

		</script>

		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec3 vWorldPosition;
			varying vec3 vViewPosition;
			varying vec3 vNormal;
			varying float percent;
			
			uniform float uDisplacementScale;
			uniform float landScheme;
			
			vec4 mod289(vec4 x)
			{
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}
			vec3 mod289(vec3 x)
			{
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}
			vec4 permute(vec4 x)
			{
			  return mod289(((x*34.0)+1.0)*x);
			}
			vec4 taylorInvSqrt(vec4 r)
			{
			  return 1.79284291400159 - 0.85373472095314 * r;
			}
			vec2 fade(vec2 t) {
			  return t*t*t*(t*(t*6.0-15.0)+10.0);
			}
			vec3 fade(vec3 t) {
			  return t*t*t*(t*(t*6.0-15.0)+10.0);
			}
			float pnoise(vec2 P, vec2 rep)
			{
			  vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
			  vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
			  Pi = mod(Pi, rep.xyxy); // To create noise with explicit period
			  Pi = mod289(Pi);        // To avoid truncation effects in permutation
			  vec4 ix = Pi.xzxz;
			  vec4 iy = Pi.yyww;
			  vec4 fx = Pf.xzxz;
			  vec4 fy = Pf.yyww;

			  vec4 i = permute(permute(ix) + iy);

			  vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0 ;
			  vec4 gy = abs(gx) - 0.5 ;
			  vec4 tx = floor(gx + 0.5);
			  gx = gx - tx;

			  vec2 g00 = vec2(gx.x,gy.x);
			  vec2 g10 = vec2(gx.y,gy.y);
			  vec2 g01 = vec2(gx.z,gy.z);
			  vec2 g11 = vec2(gx.w,gy.w);

			  vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
			  g00 *= norm.x;  
			  g01 *= norm.y;  
			  g10 *= norm.z;  
			  g11 *= norm.w;  

			  float n00 = dot(g00, vec2(fx.x, fy.x));
			  float n10 = dot(g10, vec2(fx.y, fy.y));
			  float n01 = dot(g01, vec2(fx.z, fy.z));
			  float n11 = dot(g11, vec2(fx.w, fy.w));

			  vec2 fade_xy = fade(Pf.xy);
			  vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
			  float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
			  return 2.3 * n_xy;
			}
			float pnoise(vec3 P, vec3 rep)
			{
			  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
			  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
			  Pi0 = mod289(Pi0);
			  Pi1 = mod289(Pi1);
			  vec3 Pf0 = fract(P); // Fractional part for interpolation
			  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
			  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
			  vec4 iy = vec4(Pi0.yy, Pi1.yy);
			  vec4 iz0 = Pi0.zzzz;
			  vec4 iz1 = Pi1.zzzz;

			  vec4 ixy = permute(permute(ix) + iy);
			  vec4 ixy0 = permute(ixy + iz0);
			  vec4 ixy1 = permute(ixy + iz1);

			  vec4 gx0 = ixy0 * (1.0 / 7.0);
			  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
			  gx0 = fract(gx0);
			  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
			  vec4 sz0 = step(gz0, vec4(0.0));
			  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
			  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

			  vec4 gx1 = ixy1 * (1.0 / 7.0);
			  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
			  gx1 = fract(gx1);
			  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
			  vec4 sz1 = step(gz1, vec4(0.0));
			  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
			  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

			  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
			  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
			  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
			  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
			  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
			  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
			  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
			  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

			  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
			  g000 *= norm0.x;
			  g010 *= norm0.y;
			  g100 *= norm0.z;
			  g110 *= norm0.w;
			  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
			  g001 *= norm1.x;
			  g011 *= norm1.y;
			  g101 *= norm1.z;
			  g111 *= norm1.w;

			  float n000 = dot(g000, Pf0);
			  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
			  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
			  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
			  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
			  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
			  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
			  float n111 = dot(g111, Pf1);

			  vec3 fade_xyz = fade(Pf0);
			  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
			  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
			  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
			  return 2.2 * n_xyz;
			}
			float turbulence (vec2 p , float z){
				float w  = 100.0;
				float t = -0.5;
				for(float f = 1.0; f<=10.0; f++){
					float power = pow(2.0,f);
					t+= abs(pnoise(vec3(power*p , z),vec3(2.0,2.0,100.0))/power);
				}
				return t;
			}
			void main()
			{
				vec3 displacedPosition;
				
				float height = abs(uDisplacementScale * turbulence(uv,landScheme));
				
				percent =  height / uDisplacementScale;
				displacedPosition = position + normalize( normal ) * height;
				
				
				
				vec4 mvPosition = modelViewMatrix * vec4( displacedPosition, 1.0 );
				vec4 worldPosition = modelMatrix * vec4( displacedPosition, 1.0 );
				gl_Position = projectionMatrix * mvPosition;
				vWorldPosition = worldPosition.xyz;
				vViewPosition = -mvPosition.xyz;
				vNormal = normalize( normalMatrix * normal );
			}

		</script>

		<script>
		
			if ( ! Detector.webgl ) Detector.addGetWebGLMessage();
			
			var container;

			var camera, scene, renderer;
			var uniforms, material;
			var sun, planet;
			
			var anim = true;
			
			var sphereDetail = 512;
			
			var array;
			
			init();
			animate();
		
			function init() {
			
				container = document.createElement( 'div' );
				document.body.appendChild( container );
				
				camera = new THREE.PerspectiveCamera(75, window.innerWidth/window.innerHeight, 0.0001, 1000);
				camera.position.set( 0, 0, 3 );

				scene = new THREE.Scene();
				sun = new THREE.DirectionalLight( 0xffffff);
				sun.position.set(1,1,2);
				scene.add( sun );
				type =1;
				
				uniforms = {
				
					directionalLightColor: { type: "v3", value: new THREE.Vector3(sun.color.r,sun.color.g,sun.color.b) },
					directionalLightDirection: { type: "v3", value: sun.position },
					uDisplacementScale: { type: "f", value: 0.4 },
					planetType: { type: "i", value: randScheme()  },
					landScheme : { type: "f", value: (Math.random() * 100.0)  }

				};
				
				material = new THREE.ShaderMaterial( {
					uniforms: uniforms,
					vertexShader: document.getElementById( 'vertexShader' ).textContent,
					fragmentShader: document.getElementById( 'fragmentShader' ).textContent
					
				} );
				planet = new THREE.Mesh( new THREE.SphereGeometry( 1.5,sphereDetail,sphereDetail ),  material );
				scene.add( planet );
				
				console.log(planet);
				
				
				renderer = new THREE.WebGLRenderer( { antialias: true } );
				renderer.setSize( window.innerWidth, window.innerHeight );

				container.appendChild( renderer.domElement );
				window.addEventListener( 'resize', onWindowResize, false );
			}
			
			
			function rand() {
				if(type==1){
					if(Math.random()>=.65){
						return 1*scale;
					}
					else if(Math.random()<.55){
						return -1*scale;
					}
					else{
						return 0;
						//return -1*scale;
					}
				}
				else if(type==2){
					if(Math.random()>=.5){
						return 1*scale;
					}
					else{
						return -1*scale;
					}
				}
			}
			function randScheme() {
				if(Math.random()>=.4){
					return 1;
				}
				else if(Math.random()>.2){
					return 2;
				}
				else{
					return 3;
				}
			}
			
			$(document).keydown(function(evt) {
				
				switch (evt.keyCode) 
				{
					case 87://w
						//mesh.rotation.x -= .02;
						camera.position.y += .05;
						break;
					case 65://a
						//mesh.rotation.y -= .02;
						camera.position.x -= .05;
						break;
					case 83://s
						//mesh.rotation.x += .02;
						camera.position.y -= .05;
						break;
					case 68://d
						//mesh.rotation.y += .02;
						camera.position.x += .05;
						break;
					case 73://d
						camera.position.z -= .07;
						break;
					case 79://d
						camera.position.z += .07;
						break;
					case 32://space
						anim = !anim;
						break;
					default:
						console.log(evt.keyCode);
						break;
				}
			});
			jQuery(document).ready(function($){
				$(document).bind('mousewheel', function(e){
					if(e.wheelDelta/120 > 0) {
						camera.position.z -= .07;
					}
					else{
						camera.position.z += .07;
					}
				});
			});
			
			function onWindowResize( event ) {

				//uniforms.resolution.value.x = window.innerWidth;
				//uniforms.resolution.value.y = window.innerHeight;

				renderer.setSize( window.innerWidth, window.innerHeight );

				camera.aspect = window.innerWidth / window.innerHeight;
				camera.updateProjectionMatrix();

			}
			
			function animate() {
				if(anim){
					planet.rotation.y -= .005;
				}
				requestAnimationFrame( animate );
				render();
			}
			function render() {
				renderer.render(scene, camera);
			}
		</script>
	</body>
</html>